RELEASE   = %00000001
SHIFT     = %00000010
ALT       = %00000100
CTRL      = %00001000
META      = %00010000
CAPS_LOCK = %10000000

kb_init:
  stz kb_wptr
  stz kb_rptr
  stz kb_flags
  rts

kb_handle:
  pha
  phx

  lda kb_flags
  and #RELEASE
  beq .read_key

  lda kb_flags
  eor #RELEASE
  sta kb_flags
  lda VIA1_PORTA
  cmp #$12
  beq .shfit_up
  cmp #$59
  beq .shfit_up
  jmp .return

.shfit_up:
  lda kb_flags
  eor #SHIFT
  sta kb_flags
  jmp .return

.read_key:
  lda VIA1_PORTA
  cmp #$f0
  beq .key_released
  cmp #$12
  beq .shift_down
  cmp #$59
  beq .shift_down
  cmp #$58
  beq .caps_lock_down
  cmp #$76
  beq .esc_down
  cmp #$66
  beq .backspace_down
  cmp #$5a
  beq .enter_down

  tax
  lda kb_flags
  and #SHIFT
  bne .shifted_key
  lda kb_flags
  and #CAPS_LOCK
  bne .shifted_key

  lda keymap,x
  jmp .push_key

.shifted_key:
  lda keymap_shifted,x

.push_key:
  ldx kb_wptr
  sta kb_buffer,x
  inc kb_wptr
  jmp .return

.shift_down:
  lda kb_flags
  ora #SHIFT
  sta kb_flags
  jmp .return

.caps_lock_down:
  lda kb_flags
  eor #CAPS_LOCK
  sta kb_flags
  jmp .return
  
.esc_down:
  jsr lcd_clear
  jmp .return

.backspace_down:
  jsr lcd_cursor_left
  lda #" "
  jsr lcd_print_char
  jsr lcd_cursor_left
  jmp .return

.enter_down:
  lda lcd_addr
  adc #$40
  and #$40
  ; lda #$40
  jsr lcd_set_ddram_addr
  jmp .return

.key_released:
  lda kb_flags
  ora #RELEASE
  sta kb_flags

.return:
  plx
  pla
  rts

    .org $fd00
keymap:
  ;      0123456789abcdef
  .byte "????????????? `?" ; 0
  .byte "?????q1???zsaw2?" ; 1
  .byte "?cxde43?? vftr5?" ; 2
  .byte "?nbhgy6???mju78?" ; 3
  .byte "?,kio09??./l;p-?" ; 4
  .byte "??'?[=?????]?\??" ; 5
  .byte "?????????1?47???" ; 6
  .byte "0.2568???+3-*9??" ; 7
  .byte "????????????????" ; 8
  .byte "????????????????" ; 9
  .byte "????????????????" ; a
  .byte "????????????????" ; b
  .byte "????????????????" ; c
  .byte "????????????????" ; d
  .byte "????????????????" ; e
  .byte "????????????????" ; f
keymap_shifted:
  ;      0123456789abcdef
  .byte "????????????? ~?" ; 0
  .byte "?????Q!???ZSAW@?" ; 1
  .byte "?CXDE$#?? VFTR%?" ; 2
  .byte "?NBHGY^???MJU&*?" ; 3
  .byte "?<KIO)(??>?L:P_?" ; 4
  .byte '??"?{+?????}?|??' ; 5
  .byte "?????????1?47???" ; 6
  .byte "0.2568???+3-*9??" ; 7
  .byte "????????????????" ; 8
  .byte "????????????????" ; 9
  .byte "????????????????" ; a
  .byte "????????????????" ; b
  .byte "????????????????" ; c
  .byte "????????????????" ; d
  .byte "????????????????" ; e
  .byte "????????????????" ; f