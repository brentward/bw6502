Name     Memory Decoder with VIAx2, ACIA, and Vera;
PartNo   BW-DEC-1 ;
Date     5/6/2021 ;
Revision 01 ;
Designer Brent Bettis ;
Company  Brentward Industries Ltd. ;
Assembly None ;
Location Mill Creek, WA, USA ;
Device   p22V10 ;

/* *************** INPUT PINS *********************/
PIN 1   = CLK                     ; /*                                 */ 
PIN 2   = RW                      ; /*                                 */ 
PIN 3   = A15                     ; /*                                 */ 
PIN 4   = A14                     ; /*                                 */ 
PIN 5   = A13                     ; /*                                 */ 
PIN 6   = A12                     ; /*                                 */ 
PIN 7   = A11                     ; /*                                 */ 
PIN 8   = A10                     ; /*                                 */ 
PIN 9   = A9                      ; /*                                 */ 
PIN 10  = A8                      ; /*                                 */ 
PIN 11  = A7                      ; /*                                 */ 
PIN 13  = A6                      ; /*                                 */ 
PIN 14  = A5                      ; /*                                 */ 
PIN 15  = A4                      ; /*                                 */ 

/* *************** OUTPUT PINS *********************/
PIN 23  = OE                      ; /*                                 */ 
PIN 22  = WE                      ; /*                                 */ 
PIN 21  = RAM_CS                  ; /*                                 */ 
PIN 20  = ROM_CS                  ; /*                                 */ 
PIN 19  = VIA1_CS                 ; /*                                 */ 
PIN 18  = VIA2_CS                 ; /*                                 */ 
PIN 17  = ACIA_CS                 ; /*                                 */ 
PIN 16  = VERA_CS                 ; /*                                 */ 

/* *************** LOCAL VARIABLES *****************/
FIELD Address = [A15..A4];

/* *************** LOGIC ***************************/

RAM         = Address:[0000..7FFF];
VIA1_SHADOW = Address:[0200..020F];
VIA2_SHADOW = Address:[0210..021F];
ACIA_SHADOW = Address:[0220..022F];
VERA_SHADOW = Address:[0230..024F];
ROM         = Address:[8000..FFFF];

!WE       = CLK & !RW;
!OE       = CLK & RW;
!RAM_CS   = RAM & !(VIA1_SHADOW # VIA2_SHADOW # ACIA_SHADOW # VERA_SHADOW);
!ROM_CS   = ROM;
VIA1_CS   = VIA1_SHADOW;
VIA2_CS   = VIA2_SHADOW;
ACIA_CS   = ACIA_SHADOW;
VERA_CS   = VERA_SHADOW;




